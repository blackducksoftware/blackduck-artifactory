buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}

plugins {
    id 'groovy'
}

version = '6.0.0-SNAPSHOT'
def artifactoryMinimumVersion = '5.6.2'

apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile 'com.blackducksoftware.integration:hub-artifactory-common:0.2.3-SNAPSHOT'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    compile 'org.quartz-scheduler:quartz:2.2.1'
    compile 'org.apache.commons:commons-configuration2:2.2'
    compile 'commons-beanutils:commons-beanutils:1.9.3'

    compileOnly 'org.artifactory:artifactory-papi:' + artifactoryMinimumVersion

    testCompile 'org.artifactory:artifactory-papi:' + artifactoryMinimumVersion
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def versionFile = new File("${projectDir}/src/main/resources/version.txt")
versionFile.delete()
versionFile << version

apply from: 'generateScanConfig.gradle'

task createFinalZip(type: Zip) {
    dependsOn build, generateScanConfig
    from("${projectDir}/src/main/resources/") {
        include 'version.txt'
        include 'blackDuckCacheInspector.properties'
        into 'plugins/lib'
    }

    from("${projectDir}/build/configs/") {
        include 'blackDuckScan.properties'
        into 'plugins/lib'
    }

    from("${buildDir}/libs") {
        include '*.jar'
        exclude '*sources.jar'
        exclude '*javadoc.jar'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScan.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/inspect") {
        include 'blackDuckCacheInspector.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory") {
        include 'blackDuckPolicyEnforcer.groovy'
        into 'plugins'
    }

    from(configurations.runtime) {
        include 'hub-artifactory-common*.jar'
        into 'plugins/lib'
    }
}

springBoot { mainClassName = 'com.blackducksoftware.integration.hub.artifactory.Application' }
