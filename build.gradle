buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE'
    }
}


plugins {
    id 'groovy'
}

version = '5.0.1-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile 'com.blackducksoftware.integration:hub-artifactory-common:0.1.2'
    
    compile 'org.apache.commons:commons-configuration2:2.2'
    compile 'commons-beanutils:commons-beanutils:1.9.3'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    
    compileOnly 'org.artifactory:artifactory-papi:5.6.2'
    
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def versionFile = new File("${projectDir}/src/main/resources/version.txt")
versionFile.delete()
versionFile << version

apply from: 'generateScanConfig.gradle'

task createFinalZip(type: Zip) {
    dependsOn build, generateScanConfig
    from("${projectDir}/src/main/resources/") {
        include 'version.txt'
        include 'blackDuckCacheInspector.properties'
        into 'plugins/lib'
    }
    
    from ("${projectDir}/build/configs/") {
        include 'blackDuckScanForHub.properties'
        into 'plugins/lib'
    }

    from("${buildDir}/libs") { 
        include '*.jar' 
        exclude '*sources.jar'
        exclude '*javadoc.jar'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScanForHub.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/inspect") {
        include 'blackDuckCacheInspector.groovy'
        into 'plugins'
    }

    from(configurations.runtime) {
        exclude 'artifactory-papi*.jar'
        exclude 'commons-beanutils*.jar'
        exclude 'commons-codec*.jar'
        exclude 'commons-collection*.jar'
        exclude 'commons-compress*.jar'
        exclude 'commons-io*.jar'
        exclude 'commons-lang3*.jar'
        exclude 'groovy-all*.jar'
        exclude 'gson*.jar'
        // These should be excluded, but we cannot until Artifactory updates them
        // exclude 'httpclient*.jar'
        // exclude 'httpcore*.jar'
        exclude 'jcl-over-slf4j*.jar'
        exclude 'joda-time*.jar'
        exclude 'jul-to-slf4j*.jar'
        exclude 'log4j-over-slf4j*.jar'
        exclude 'logback*.jar'
        exclude 'snakeyaml*.jar'
        exclude 'slf4j-api*.jar'
        exclude 'spring*.jar'

        into 'plugins/lib'
    }
}

springBoot { mainClassName = 'com.blackducksoftware.integration.hub.artifactory.Application' }
