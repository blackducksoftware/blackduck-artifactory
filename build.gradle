buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
    }
}

plugins {
    id 'groovy'
    id 'org.springframework.boot' version '1.5.8.RELEASE'
    id 'org.sonarqube' version '2.6.1'
}

version = '5.0.0-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'

def versionFile = new File("${projectDir}/src/main/resources/version.txt")
versionFile.delete()
versionFile << version

task createFinalZip(type: Zip, dependsOn: build) {
    from("${projectDir}/src/main/resources/") {
        include 'version.txt'
        include 'blackDuckCacheInspector.properties'
        include 'blackDuckScanForHub.properties'
        into 'plugins/lib'
    }

    from("${buildDir}/libs") { include '*.jar' }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScanForHub.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/inspect") {
        include 'blackDuckCacheInspector.groovy'
        into 'plugins'
    }

    from(configurations.runtime) {
        exclude 'artifactory-papi*.jar'
        exclude 'commons-compress*.jar'
        exclude 'groovy-all*.jar'
        exclude 'gson*.jar'
        exclude 'logback*.jar'
        exclude 'snakeyaml*.jar'
        exclude 'spring*.jar'
        into 'plugins/lib'
    }
}

repositories {
	mavenLocal()
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:26.0.0'
    compile 'com.blackducksoftware.integration:hub-artifactory-common:0.0.12'
    
    compile 'org.apache.commons:commons-configuration2:2.2'
    compile 'commons-beanutils:commons-beanutils:1.9.3'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'

    compileOnly 'org.artifactory:artifactory-papi:5.6.2'
	compileOnly 'com.google.guava:guava:21.0'
    
    // These dependencies are introduced by the Artifactory Public API and are only included here to resolve vulnerability conflicts
    // Please update the PAPI and remove as soon as a more secure version of the PAPI is released - rotte (Feb. 20, 2018)
    // updated from 2.8.1
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.8.11.1'
    // updated from 1.3.1
    compileOnly 'com.thoughtworks.xstream:xstream:1.4.10'
    
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClass = 'com.blackducksoftware.integration.hub.artifactory.Application' }

sonarqube {
    properties {
        property 'sonar.projectName', 'Integrations - Hub Artifactory'
        property 'sonar.projectKey', 'com.blackducksoftware:int:hub-artifactory'
        property 'sonar.sources', sourceSets.main.groovy.srcDirs
        property 'sonar.verbose', false
    }
}