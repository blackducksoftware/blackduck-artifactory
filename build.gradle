buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE'
        classpath 'org.apache.commons:commons-configuration2:2.2'
        classpath 'commons-beanutils:commons-beanutils:1.9.3'
        classpath 'com.blackducksoftware.integration:hub-artifactory-common:0.0.17'
    }
}
import org.apache.commons.configuration2.PropertiesConfiguration
import org.apache.commons.configuration2.builder.fluent.Configurations
import org.apache.commons.configuration2.io.FileHandler
import com.blackducksoftware.integration.hub.artifactory.PluginProperty
import org.joda.time.DateTime
import org.joda.time.DateTimeZone
import org.joda.time.format.DateTimeFormat

plugins {
    id 'groovy'
    id 'org.springframework.boot' version '1.5.10.RELEASE'
}

version = '5.0.0-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'

repositories {
    mavenLocal()
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-artifactory-common:0.0.20-SNAPSHOT'
    
    compile 'org.apache.commons:commons-configuration2:2.2'
    compile 'commons-beanutils:commons-beanutils:1.9.3'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    
    compile 'org.quartz-scheduler:quartz:2.2.1'
    compileOnly 'org.artifactory:artifactory-papi:5.6.2'
    
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def versionFile = new File("${projectDir}/src/main/resources/version.txt")
versionFile.delete()
versionFile << version

task generateScanConfig {
    doLast {
        def configs = new Configurations()
        def scanConfigFile = new File("${projectDir}/src/main/resources/blackDuckScanForHub.properties")
        PropertiesConfiguration scanConfig = configs.properties(scanConfigFile)
        def dateTimePattern = scanConfig.getString(PluginProperty.HUB_ARTIFACTORY_SCAN_DATE_TIME_PATTERN.getKey())
        String nowString = DateTime.now().toLocalDate().toDateTimeAtStartOfDay(DateTimeZone.UTC).minusYears(1).toString(DateTimeFormat.forPattern(dateTimePattern).withZoneUTC())
        scanConfig.setProperty(PluginProperty.HUB_ARTIFACTORY_SCAN_CUTOFF_DATE.getKey(), nowString)
        FileHandler handler = new FileHandler(scanConfig)
        handler.save(new File("${projectDir}/build/configs/blackDuckScanForHub.properties"))
    }
}


task createFinalZip(type: Zip) {
    dependsOn build, generateScanConfig
    from("${projectDir}/src/main/resources/") {
        include 'version.txt'
        include 'blackDuckCacheInspector.properties'
        into 'plugins/lib'
    }
    
    from ("${projectDir}/build/configs/") {
        include 'blackDuckScanForHub.properties'
        into 'plugins/lib'
    }

    from("${buildDir}/libs") { 
        include '*.jar' 
        exclude '*sources.jar'
        exclude '*javadoc.jar'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScanForHub.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/inspect") {
        include 'blackDuckCacheInspector.groovy'
        into 'plugins'
    }

    from(configurations.runtime) {
        exclude 'artifactory-papi*.jar'
        exclude 'commons-beanutils*.jar'
        exclude 'commons-codec*.jar'
        exclude 'commons-collection*.jar'
        exclude 'commons-compress*.jar'
        exclude 'commons-io*.jar'
        exclude 'commons-lang3*.jar'
        exclude 'groovy-all*.jar'
        exclude 'gson*.jar'
        exclude 'httpclient*.jar'
        exclude 'httpcore*.jar'
        exclude 'jcl-over-slf4j*.jar'
        exclude 'joda-time*.jar'
        exclude 'jul-to-slf4j*.jar'
        exclude 'log4j-over-slf4j*.jar'
        exclude 'logback*.jar'
        exclude 'quartz*.jar'
        exclude 'snakeyaml*.jar'
        exclude 'slf4j-api*.jar'
        exclude 'spring*.jar'

        into 'plugins/lib'
    }
}

springBoot { mainClass = 'com.blackducksoftware.integration.hub.artifactory.Application' }
