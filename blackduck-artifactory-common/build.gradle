buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.synopsys.integration:common-gradle-plugin:1.1.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'groovy'
}

apply plugin: 'com.synopsys.integration.simple'
apply plugin: 'kotlin'

jar.finalizedBy shadowJar

dependencies {
    implementation "com.blackducksoftware.integration:blackduck-common:$blackDuckCommonVersion"
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    compileOnly "org.artifactory:artifactory-papi:$artifactoryMinimumVersion"
    testCompile "org.artifactory:artifactory-papi:$artifactoryMinimumVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'com.github.kittinunf.fuel:fuel:2.1.0'
    testImplementation 'com.github.kittinunf.fuel:fuel-gson:2.1.0'
}

license {
    header rootProject.file('LICENSE')
}

shadowJar {
    dependencies {
        exclude(dependency('org.artifactory:artifactory-papi:.*'))
        exclude(dependency('org.codehaus.groovy:groovy-all:.*'))
    }
    // Relocating is powerful and should be used judiciously.
    // This list should be updated only when we hit a noClassDefFound or noSuchMethodException due to clashing dependencies between us and Artifactory
    relocate('org.apache.commons.lang3', 'embedded.org.apache.commons.lang3')
    relocate('org.apache.http', 'embedded.org.apache.http')

    relocate('com.jayway.jsonpath', 'embedded.com.jayway.jsonpath')
    relocate('net.minidev', 'embedded.net.minidev')
    relocate('org.ow2.asm', 'embedded.org.ow2.asm')

    classifier null
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
