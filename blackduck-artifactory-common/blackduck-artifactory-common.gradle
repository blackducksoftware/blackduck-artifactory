plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

jar.finalizedBy shadowJar

final String artifactoryMinimumVersion = '5.6.2'

dependencies {
    implementation 'com.blackducksoftware.integration:blackduck-common:42.3.0'

    compileOnly 'org.artifactory:artifactory-papi:' + artifactoryMinimumVersion

    testCompile 'org.artifactory:artifactory-papi:' + artifactoryMinimumVersion

    testImplementation 'com.blackducksoftware.integration:integration-test-common:4.+'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

license {
    header rootProject.file('LICENSE')
}

shadowJar {
    dependencies {
        exclude(dependency('org.artifactory:artifactory-papi:.*'))
        exclude(dependency('org.codehaus.groovy:groovy-all:.*'))
    }
    // Relocating is powerful and should be used judiciously.
    // This list should be updated only when we hit a noClassDefFound or noSuchMethodException due to clashing dependencies between us and Artifactory
    relocate('org.apache.commons.lang3', 'embedded.org.apache.commons.lang3')
    relocate('org.apache.http', 'embedded.org.apache.http')

    relocate('com.jayway.jsonpath', 'embedded.com.jayway.jsonpath')
    relocate('net.minidev', 'embedded.net.minidev')
    relocate('org.ow2.asm', 'embedded.org.ow2.asm')

    classifier null
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
