buildscript {
    ext.kotlin_version = '1.3.31'
    ext.spring_boot_version = '2.1.0.RELEASE'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: 'kotlin'
apply plugin: "kotlin-spring" // https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = parent?.group ?: "com.synopsys.integration"
version = parent?.version ?: "UNKNOWN-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation project(":blackduck-artifactory-common")

    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    implementation 'org.artifactory:artifactory-papi:' + ext.artifactoryMinimumVersion
    implementation "com.blackducksoftware.integration:blackduck-common:43.0.0"
    implementation 'com.github.kittinunf.fuel:fuel:2.1.0'
    implementation 'com.github.kittinunf.fuel:fuel-gson:2.1.0'

    implementation("org.springframework.boot:spring-boot-starter")

    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.4.2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.4.2'
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

compileKotlin {
    kotlinOptions {
        suppressWarnings = true
    }
}

springBoot {
    mainClassName = 'org.synopsys.integration.blackduck.artifactory.automation.SpringApplicationKt'
}

test {
    useJUnitPlatform() {
        excludeTags 'automation'
    }
}

task automation(type: Test) {
    useJUnitPlatform {
        includeTags 'automation'
    }
    check.dependsOn it
    shouldRunAfter test
}