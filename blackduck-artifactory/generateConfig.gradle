buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'org.apache.commons:commons-configuration2:2.2'
        classpath 'commons-beanutils:commons-beanutils:1.9.3'
        classpath 'joda-time:joda-time:2.10'
    }
}

import org.apache.commons.configuration2.PropertiesConfiguration
import org.apache.commons.configuration2.builder.fluent.Configurations
import org.apache.commons.configuration2.io.FileHandler
import org.joda.time.DateTime
import org.joda.time.DateTimeZone
import org.joda.time.format.DateTimeFormat

final pluginPropertiesFileName = 'blackDuckPlugin.properties'
final String dateTimePatternKey = 'blackduck.date.time.pattern'
final String scanCutoffDateKey = 'blackduck.artifactory.scan.cutoff.date'

task generateConfig {
    doLast {
        def configs = new Configurations()
        def pluginConfigFile = new File("${projectDir}/src/main/resources/$pluginPropertiesFileName")
        PropertiesConfiguration propertiesConfiguration = configs.properties(pluginConfigFile)
        def dateTimePattern = propertiesConfiguration.getString(dateTimePatternKey)
        String nowString = DateTime.now().toLocalDate().toDateTimeAtStartOfDay(DateTimeZone.UTC).minusYears(1).toString(DateTimeFormat.forPattern(dateTimePattern).withZoneUTC())
        propertiesConfiguration.setProperty(scanCutoffDateKey, nowString)
        FileHandler handler = new FileHandler(propertiesConfiguration)
        handler.save(new File("${projectDir}/build/configs/$pluginPropertiesFileName"))
    }
}
